<html>
<body><pre>
&lt;?xml version="1.0" encoding="ISO-8859-1"?>

&lt;xsl:stylesheet 
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
  version="1.0"
  >
  &lt;xsl:output method="html"/> 
  &lt;xsl:strip-space elements="SECT"/>

  &lt;xsl:template match="/">
    &lt;html>&lt;body>
       &lt;xsl:apply-templates/>
    &lt;/body>&lt;/html>
  &lt;/xsl:template>

  &lt;xsl:template match="/ARTICLE/TITLE">
    &lt;h1 align="center"> &lt;xsl:apply-templates/> &lt;/h1>
  &lt;/xsl:template>

  &lt;!-- Top Level Heading -->
  &lt;xsl:template match="/ARTICLE/SECT">
      &lt;h2> &lt;xsl:apply-templates select="text()|B|I|U|DEF|LINK"/> &lt;/h2>
      &lt;xsl:apply-templates select="SECT|PARA|LIST|NOTE"/>
  &lt;/xsl:template>
    
  &lt;!-- Second-Level Heading -->
  &lt;xsl:template match="/ARTICLE/SECT/SECT">
      &lt;h3> &lt;xsl:apply-templates select="text()|B|I|U|DEF|LINK"/> &lt;/h3>
      &lt;xsl:apply-templates select="SECT|PARA|LIST|NOTE"/>
  &lt;/xsl:template>

  &lt;!-- Third-Level Heading -->
  &lt;xsl:template match="/ARTICLE/SECT/SECT/SECT">
     &lt;xsl:message terminate="yes">Error: Sections can only be nested 2 deep.&lt;/xsl:message>
  &lt;/xsl:template>

  &lt;!-- Paragraph -->
  &lt;xsl:template match="PARA">
    &lt;p> &lt;xsl:apply-templates select="text()|B|I|U|DEF|LINK"/> &lt;/p>
    &lt;xsl:apply-templates select="PARA|LIST|NOTE"/>
  &lt;/xsl:template>

  &lt;!-- Text -->
&lt;!-- 
  &lt;xsl:template match="text()">
    &lt;xsl:value-of select="normalize-space()"/>
  &lt;/xsl:template>
-->

  &lt;!-- LIST  -->
  &lt;xsl:template match="LIST">
    &lt;xsl:if test="@type='ordered'">
      &lt;ol>
      &lt;xsl:apply-templates/>
      &lt;/ol>
    &lt;/xsl:if>
    &lt;xsl:if test="@type='unordered'">
      &lt;ul>
      &lt;xsl:apply-templates/>
      &lt;/ul>
    &lt;/xsl:if>
  &lt;/xsl:template>

  &lt;!-- list ITEM -->
  &lt;xsl:template match="ITEM">
    &lt;li>&lt;xsl:apply-templates/>
    &lt;/li>
  &lt;/xsl:template>

  &lt;xsl:template match="NOTE">
    &lt;blockquote>&lt;b>Note:&lt;/b>&lt;br/>
      &lt;xsl:apply-templates/>
    &lt;/blockquote>
  &lt;/xsl:template>

  &lt;xsl:template match="DEF">
     &lt;i> &lt;xsl:apply-templates/> &lt;/i>   
  &lt;/xsl:template>

  &lt;xsl:template match="B|I|U">
     &lt;xsl:element name="{name()}">
       &lt;xsl:apply-templates/>
     &lt;/xsl:element> 
  &lt;/xsl:template>

  &lt;xsl:template match="LINK">
    &lt;xsl:if test="@target">
      &lt;!--Target attribute specified.-->
      &lt;xsl:call-template name="htmLink">
        &lt;xsl:with-param name="dest" select="@target"/>  &lt;!--Destination = attribute value-->
      &lt;/xsl:call-template>
    &lt;/xsl:if>

    &lt;xsl:if test="not(@target)">
      &lt;!--Target attribute not specified.-->
      &lt;xsl:call-template name="htmLink">
        &lt;xsl:with-param name="dest">
          &lt;xsl:apply-templates/>  &lt;!--Destination value = text of node-->
        &lt;/xsl:with-param>
      &lt;/xsl:call-template>
    &lt;/xsl:if>
  &lt;/xsl:template>

  &lt;!-- A named template that constructs an HTML link -->
  &lt;xsl:template name="htmLink">
    &lt;xsl:param name="dest" select="UNDEFINED"/> &lt;!--default value-->
    &lt;xsl:element name="a">
      &lt;xsl:attribute name="href">
        &lt;xsl:value-of select="$dest"/> &lt;!--link target-->
      &lt;/xsl:attribute>
      &lt;xsl:apply-templates/> &lt;!--name of the link from text of node-->
    &lt;/xsl:element> 
  &lt;/xsl:template>

&lt;/xsl:stylesheet>


</pre></body>
</html>
